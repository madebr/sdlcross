cmake_minimum_required(VERSION 3.0)
project(sdlcross C)

option(WITH_IMAGE "Enable SDL2_image" ON)
option(WITH_MIXER "Enable SDL2_mixer" ON)

find_package(SDL2 REQUIRED CONFIG)
find_package(SDL2_image CONFIG)
find_package(SDL2_mixer CONFIG)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/cmake")
if(NOT SDL2_image_FOUND)
    find_package(SDL2_image REQUIRED MODULE)
endif()
if(NOT SDL2_mixer_FOUND)
    find_package(SDL2_mixer REQUIRED MODULE)
endif()

if(ANDROID)
    enable_language(CXX)
    add_library(sdlcross SHARED main.c)
    set_target_properties(sdlcross PROPERTIES LINKER_LANGUAGE CXX)
else()
    add_executable(sdlcross main.c)
endif()

target_link_libraries(sdlcross PRIVATE SDL2::SDL2main)
if(WITH_IMAGE)
    target_compile_definitions(sdlcross PRIVATE WITH_IMAGE)
    target_link_libraries(sdlcross PRIVATE SDL2_image::SDL2_image)
endif()
if(WITH_MIXER)
    target_compile_definitions(sdlcross PRIVATE WITH_MIXER)
    target_link_libraries(sdlcross PRIVATE SDL2_mixer::SDL2_mixer)
endif()
target_link_libraries(sdlcross PRIVATE SDL2::SDL2)
set_target_properties(sdlcross PROPERTIES
    CXX_STANDARD 11
)
if(NOT MSVC)
    target_compile_options(sdlcross PRIVATE -Wall -Wextra -Werror)
endif()

if(ANDROID)
    if(WITH_IMAGE)
        # Explicitly link to libpng to force libpng16.so to be added to the .apk
        # FIXME: how can this be avoided?
        if(TARGET SDL2_image::external_libpng)
            target_link_libraries(sdlcross PRIVATE SDL2_image::external_libpng)
        endif()
    endif()
    if(WITH_MIXER)
        if(TARGET SDL2_mixer::external_libflac)
            target_link_libraries(sdlcross PRIVATE SDL2_mixer::external_libflac)
        endif()
        if(TARGET SDL2_mixer::external_libopusfile)
            target_link_libraries(sdlcross PRIVATE SDL2_mixer::external_libopusfile)
        endif()
        if(TARGET SDL2_mixer::external_libmpg123)
            target_link_libraries(sdlcross PRIVATE SDL2_mixer::external_libmpg123)
        endif()
    endif()
endif()
