cmake_minimum_required(VERSION 3.0)
project(sdlcross C)

option(TARGET_SDL3 "Use SDL3" OFF)

if(TARGET_SDL3)
    find_package(SDL3 REQUIRED CONFIG)
else()
    find_package(SDL2 REQUIRED CONFIG)
    find_package(SDL2_image CONFIG)
    find_package(SDL2_mixer CONFIG)

    list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/cmake")
    if(NOT SDL2_image_FOUND)
        find_package(SDL2_image REQUIRED)
    endif()
    if(NOT SDL2_mixer_FOUND)
        find_package(SDL2_mixer REQUIRED)
    endif()
endif()

if(ANDROID)
    enable_language(CXX)
    add_library(sdlcross SHARED main.c)
    set_target_properties(sdlcross PROPERTIES LINKER_LANGUAGE CXX)
else()
    add_executable(sdlcross main.c)
endif()

if(TARGET_SDL3)
    target_compile_definitions(sdlcross PRIVATE TARGET_SDL3)
    target_link_libraries(sdlcross PRIVATE SDL3::SDL3)
else()
    target_link_libraries(sdlcross PRIVATE SDL2::SDL2main SDL2_mixer::SDL2_mixer SDL2_image::SDL2_image SDL2::SDL2)
endif()
set_target_properties(sdlcross PROPERTIES
    CXX_STANDARD 11
)
if(NOT MSVC)
    target_compile_options(sdlcross PRIVATE -Wall -Wextra -Werror)
endif()

if(ANDROID)
    if(TARGET_SDL3)
    else()
        # Explicitly link to libpng to force libpng16.so to be added to the .apk
        # FIXME: how can this be avoided?
        if(TARGET SDL2_image::external_libpng)
            target_link_libraries(sdlcross PRIVATE SDL2_image::external_libpng)
        endif()
        if(TARGET SDL2_mixer::external_libflac)
            target_link_libraries(sdlcross PRIVATE SDL2_mixer::external_libflac)
        endif()
        if(TARGET SDL2_mixer::external_libopusfile)
            target_link_libraries(sdlcross PRIVATE SDL2_mixer::external_libopusfile)
        endif()
        if(TARGET SDL2_mixer::external_libmpg123)
            target_link_libraries(sdlcross PRIVATE SDL2_mixer::external_libmpg123)
        endif()
    endif()
endif()
